plugins {
	id 'java'
	id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
	id 'maven-publish'
}

archivesBaseName = "${mod_name}-common-${minecraft_version}"

minecraft {
	version(minecraft_version)
	runs {   
		if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {
			server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
				workingDirectory(this.file("run"))
			}
			client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
				workingDirectory(this.file("run"))
			}
		}
	}
}

dependencies {
	compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'

	compileOnly "net.minecraftforge:forgeconfigapiport-fabric:4.2.11"
	// this is a forge dependency (with unobfuscated class names); OPaC doesn't publish dev jars
	compileOnly group: 'curse.maven', name: 'openpac-636608', version: '4021875', changing: true
	compileOnly group: 'info.journeymap', name: 'journeymap-api', version: '1.19.1-1.9-SNAPSHOT', changing: true
}

processResources {
	def buildProps = project.properties.clone()

	filesMatching(['pack.mcmeta']) {
		expand buildProps
	}
}
